import java.time.LocalDateTime
import java.time.temporal.ChronoUnit

plugins {
    id 'java'
    id 'jacoco'
    id 'io.quarkus'
    id 'org.sonarqube'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '5.2.2'
    id "com.diffplug.spotless" version "6.25.0"
    id "com.github.vlsi.jandex" version "1.90"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply from: rootProject.file('gradle/install-git-hooks.gradle')

allprojects {
    group = project.group
    version = project.version + "-" + getCheckedOutGitCommitHash()
}

repositories {
    mavenCentral()
    mavenLocal()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/EFA-FHB/eforms-validator-core"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    // Quarkus
    implementation(platform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}"))
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-resteasy'
    implementation 'io.quarkus:quarkus-resteasy-jackson'
    implementation 'io.quarkus:quarkus-hibernate-validator'
    implementation 'io.quarkus:quarkus-resteasy-multipart'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    // Utils
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.13.0'

    implementation 'org.projectlombok:lombok:1.18.22'

    implementation 'com.jcabi:jcabi-xml:0.29.0'
    implementation 'com.helger.schematron:ph-schematron-pure:7.1.3'
    implementation 'com.github.vladimir-bukhtoyarov:bucket4j-core:7.6.0'

    implementation 'com.opencsv:opencsv:5.8'

    compileOnly 'org.projectlombok:lombok:1.18.22'

    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'io.rest-assured:rest-assured:3.0.0'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.16.1'
    constraints {
        implementation('org.yaml:snakeyaml:2.0') {
            because 'version 1.33 is vulnerable to CVE-2022-1471'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    reports.html.required = false
    minHeapSize = "1024m" // initial heap size
    maxHeapSize = "7168m" // maximum heap size
}

compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.register('buildInfo') {
    doLast {
        def commitId = System.getenv('GIT_SHA_REF') ?: grgit.head().getId()

        new File(sourceSets.main.output.resourcesDir, "build_info.json").text =
                """{
    "name" : "${rootProject.name}-${project.name}",
    "build" : {
        "version" : "${project.version}",
        "time" : "${LocalDateTime.now().truncatedTo(ChronoUnit.SECONDS)}",
        "git" : {
            "sha" : "${commitId}",
            "repository_url" : "${grgit.remote.list().get(0).url}"
        }
    }
}
"""
    }
}


def getCheckedOutGitCommitHash() {
    grgit.head().abbreviatedId
}

sonarqube {
    String sonarHostUrl = System.getenv("SONAR_HOST_URL") ?: property("sonarHostUrl")
    String sonarLogin = System.getenv("SONAR_TOKEN") ?: property("sonarLogin")
    String sonarPassword = System.getenv("SONAR_TOKEN") ? "" : property("sonarPassword")
    properties {
        property 'sonar.host.url', sonarHostUrl
        property 'sonar.login', sonarLogin
        property 'sonar.password', sonarPassword
        property "sonar.projectKey", "EFA-FHB_eforms-validator-core"
        property "sonar.projectName", "eforms-validator-core"
        property "sonar.organization", "efa-fhb"
        property "sonar.language", "java"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/jacoco-report/jacoco.xml"
        property 'sonar.inclusions', "**/service/**, **/enums/**, **/validation/**"
    }
}

tasks.named('jacocoTestReport').configure {
    dependsOn('processJandexIndex')
}

test.finalizedBy jacocoTestReport

jacocoTestReport {
    dependsOn processJandexIndex
    getExecutionData().setFrom(fileTree(buildDir).include("jacoco/*.exec"))
    reports {
        xml.required = true
    }
}

tasks.named('sonarqube').configure {
    dependsOn jacocoTestReport
}

spotless {
    ratchetFrom 'origin/main'
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

tasks.named('compileJava').configure {
    dependsOn buildInfo
}

tasks.named('quarkusDependenciesBuild').configure {
    dependsOn('processJandexIndex')
    mustRunAfter('processJandexIndex')
}

shadowJar {
    archiveClassifier.set('fat')
    configurations = [project.configurations.runtimeClasspath]
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Notice Validator',
                'Implementation-Version': project.version,
                'Main-Class': 'com.nortal.efafhb.eforms.validator.Main'
    }
}
